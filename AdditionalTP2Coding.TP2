
// These two lines were created by CamDawg. It's a new state that checks if NPC is able to talk. USE this in your mod. See the dialogue for details.
APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~
UNLESS ~CD_STATE_NOTVALID~

COPY ~Erevain/Audio~ ~override~ // All Erevain's audio files are in the game now, and we'll hear them when she talks.

// I am going to use my prefix now. For the purposes of this mod, it is going to be O#. DON'T USE IT, REGISTER YOUR OWN!
// Go here: http://forums.blackwyrmlair.net/index.php?showtopic=113 - the list of prefixes is in the first post of the thread. Use it in ALL your files and ALL variables.

// Remember that all your filenames should not be longer than 8 symbols. Hence I often use O#Bran, and not O#Erevain(9 symbols). 
// You can use long character's names in variables, if you want. But I use variable names like O#BranSomething instead of O#ErevainSomething, so it's easier for you to mass-replace(read below).

// HOW TO DO A QUICK MASS-REPLACE WHEN YOU'VE GOT YOUR OWN PREFIX?

// Imagine you have an NPC called Anna, and your prefix is BB(and, yes, this prefix is taken, too, it's just hypothetical).

// To use my code for your NPC, replace all o#bran with bbanna(if you want it to be case-sensitive, then o#bran with bbanna, O#Bran with BBAnna and O#BRAN with BBANNA).
// Exceptions are ToB files(with 25's in it), where you have to replace o#bran with bbanna, and THEN o#bra with bbann(files can be only eight symbols long, so I have to use O#BBRA25.d, not O#BBRAN25.d).
// Naturally, make backups before you mass-replace.

// This is my creature file, my .cre file. How to create it? Create a character in the game, export him, open him via Shadowkeeper and convert him to .cre. Or copy an existing .cre file. Near Infinity and DLTCEP help you edit your .cre.
COPY ~Erevain/Characters/O#Bran.cre~ ~override/O#Bran.cre~ // copy our .cre file to the game. Below I edit(patch) O#Bran.cre, adding a soundset, a bio, a dialogue file and other nice things.
SAY NAME1 ~Erevain~
SAY NAME2 ~Erevain~
SAY INITIAL_MEETING ~By the ice breath of Auril! 'Tis good to see new faces and taste freedom again.~ [o#bran01] // This text in square brackets means that BRANW01.wav is assigned here.
SAY MORALE ~Tempus, forgive my cowardice.~ [o#bran02]
SAY HAPPY ~I am proud to stand among such strong warriors.~ [o#bran04] 
SAY UNHAPPY_ANNOYED ~Our actions are not those of warrior born.~ [o#bran05] 
SAY UNHAPPY_SERIOUS ~What sort of craven cowards are we?~ [o#bran06]
SAY UNHAPPY_BREAKING ~I can no longer honorably work with such as you.~ [o#bran07]
SAY LEADER ~I have always dreamt of leading such a worthy band.~ [o#bran08]
SAY TIRED ~I'm tired.~ [o#bran09]
SAY BORED ~Damn the icicles of Auril. I'm BORED.~ [o#bran10]
SAY BATTLE_CRY1 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY2 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY3 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY4 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY5 ~Tempus! Give us victory!~ [o#bran03] // Erevain BG1 has very few sound files, so I have to use the same line over and over. You can use five different ones, however.
SAY DAMAGE ~~ [o#bran38] // There's no text when Erevain cries out in pain.
SAY DYING ~~ [o#bran39]
SAY HURT ~'Tis but a flesh wound.~ [o#bran11]
SAY AREA_FOREST ~I am always at ease in the forest. It reminds me of home.~  // If there's no sound file for the line, then it's going to be silent.
SAY AREA_CITY ~By Tempus, I love the big city!~
SAY AREA_DUNGEON ~Danger is about. Be wary, friends.~
SAY AREA_DAY ~Ah, I love traveling in fair weather.~
SAY AREA_NIGHT ~I can't see a thing. Are we craven thieves that we do not sleep?~
SAY SELECT_COMMON1 ~By Tempus' shield.~ [o#bran17]
SAY SELECT_COMMON2 ~What is your command?~ [o#bran18]
SAY SELECT_COMMON3 ~Hello?~ [o#bran19]
SAY SELECT_COMMON4 ~By Tempus' shield.~ [o#bran17]
SAY SELECT_COMMON5 ~What is your command?~ [o#bran18]
SAY SELECT_COMMON6 ~Hello?~ [o#bran19]
SAY SELECT_ACTION1 ~'Tis a fine day to die.~ [o#bran20]
SAY SELECT_ACTION2 ~If Tempus wills it.~ [o#bran21]
SAY SELECT_ACTION3 ~By Valkur's mighty blade.~ [o#bran22]
SAY SELECT_ACTION4 ~'Tis better to die on your feet, than live on your knees.~ [o#bran23]
SAY SELECT_ACTION5 ~May Auril bestow the frost kiss upon our enemies.~ [o#bran24]
SAY SELECT_ACTION6 ~By Valkur's strapping buttocks.~ [o#bran25]
SAY SELECT_ACTION7 ~A god's favor is fickle, at best.~ [o#bran26]
SAY SELECT_RARE1 ~To die in battle is the most glorious death of all.~ [o#bran40]
SAY SELECT_RARE2 ~Tempus would guide your wayward soul, if you let him.~ [o#bran30]
SAY CRITICAL_HIT ~Tempus!~
SAY CRITICAL_MISS ~Argh!~
SAY TARGET_IMMUNE ~Another weapon! Now!~
SAY INVENTORY_FULL ~I can carry no more. I had to drop it.~ 
SAY SPELL_DISRUPTED ~My spell!~
SAY BIO ~When asked about her past, Erevain reveals that she left her home of Seawolf in the Norheim isles at an early age. She was apparently reviled by her own people for wanting to become a priest, a station reserved exclusively for the men of the village. She harbors no resentment, realizing that her faith must face such tests or be worthless. She traveled the coast, offering her services to local militias, and eventually came into the employ of an adventuring troup in the Nashkel area. Erevain had no idea that the group was participating in banditry, however, and on their first raid she balked at attacking a caravan of unarmed merchants. She rebelled, and a mage named Tranzig turned her to stone in the ensuing fight. Despite the unfavorable outcome, Erevain feels that Tempus views her actions approvingly. To attack those that are unarmed is the basest of villainy.~
WRITE_ASCII 0x248 ~O#BranS~ #8  // override script where you put all the commands for lovetalks and such.
WRITE_ASCII 0x2cc ~O#Bran~  #8  // dialogue file where we'll put Erevain's greeting dialogue.
WRITE_ASCII 0x280 ~O#Bran~  #32 // death variable - when we want to refer to Erevain in the game, we'll call her O#Bran. For example, InParty("O#Bran") means that Erevain is in party.
WRITE_ASCII 0X34  ~BRANWES~ #8  // small portrait
WRITE_ASCII 0x3c  ~BRANWEM~ #8  // medium portrait

// Are your sounds not running? Check that all filenames are used correctly. It might also be worth it to download and use DLTCEP to convert your .wav files in .wavc(converted wav, same extension) format for the game's use.
// Most mods now use .ogg audio files, if the mod is fully voiced. For instructions, look at Xan's Xan/Command files. But if you only have thirty or forty files, .wavc format should suffice. Erevain's files are in .wavc format.

COMPILE ~Erevain/Dialogue/O#BBran.d~ // All Erevain's banters with other party members will go here.
	~Erevain/Dialogue/O#Bran.d~  // Erevain's greeting dialogue will go here. 
	~Erevain/Dialogue/O#BranJ.d~ // The rest of Erevain's dialogue: her commentary, interjections, talks with the player, lovetalks and everything else.
	~Erevain/Dialogue/O#BranP.d~ // Erevain's leaving party dialogue will go here.

	~Erevain/Scripts/O#BranS.baf~ // Erevain's script.
	~Erevain/Scripts/O#BranD.baf~ // Erevain's dream script for events at rest. If you want your NPC to talk to you at rest, add scripting here.
	~Erevain/Scripts/O#BranF.baf~ // Erevain's "fixing" script. Remember how various modders advised you to "select your NPC and press K"? Well, that's how they do it.

// We are going to spawn Erevain in the Bridge District. It means that we have to add a little bit of script to the Bridge District Area(AR0500). Note that I wrote .bcs in the first part of that line, not .baf
EXTEND_TOP ~AR0500.bcs~ ~Erevain/Scripts/AR0500.baf~

// The following lines are not needed if you're not planning to code Bodhi's abduction.
// Since Erevain is going to be romanceable, we are going to add Bodhi's abduction. The dialogue is in O#BRANJ.d file, and the rest is here:
COMPILE	~Erevain/Scripts/O#BranVC.baf~                     // Erevain's vampire cutscene.
COMPILE	~Erevain/Scripts/O#BranV.baf~                      // Erevain's vampire script.
EXTEND_TOP ~BODHIAMB.bcs~ ~Erevain/Scripts/BODHIAMB.baf~   // Extending Bodhi's script
EXTEND_TOP ~VAMPAMB.bcs~ ~Erevain/Scripts/VAMPAMB.baf~     // New script for Erevain during the abduction
EXTEND_TOP ~CLEANSE.bcs~ ~Erevain/Scripts/CLEANSE.baf~     // Extra script for the altar of Amaunator
EXTEND_TOP ~AR0809.bcs~ ~Erevain/Scripts/AR0809.baf~       // Bodhi's crypt, spawning the vampire Erevain
COPY ~Erevain/Characters/o#branv.cre~ ~override~           // Erevain's vampire creature. If you want her to look different, open her in DLTCEP and change the avatar/gender.
SAY NAME1 ~Erevain~ // These lines must follow o#branv.cre immediately, otherwise the vampire Erevain won't have a name
SAY NAME2 ~Erevain~
WRITE_ASCII 0x248 ~O#BranV~  #8  // override script
WRITE_ASCII 0x2cc ~O#BranV~  #8  // dialogue file
WRITE_ASCII 0x280 ~O#BranV~  #32 // death variable
COPY ~Erevain/Characters/o#branb.itm~ ~override~           // Erevain's body - you can create an /Items folder and place it there, too.
SAY NAME1 ~Erevain's Body~ // These lines must follow o#branb.itm immediately, otherwise her body won't have a name and a description
SAY NAME2 ~Erevain's Body~
SAY UNIDENTIFIED_DESC ~Erevain was captured by Bodhi and infected with vampirism, forcing you to slay her. There may be some way to revive her, though you do not know what it would be.~
SAY DESC ~Erevain was captured by Bodhi and infected with vampirism, forcing you to slay her. There may be some way to revive her, though you do not know what it would be.~

// Since we're using Bodhi's abduction for Erevain, with journal entries, and we want these journal entries to work for BG2:EE, we need to use this code:
ACTION_IF GAME_IS ~BG2EE~ BEGIN
ADD_JOURNAL EXISTING TITLE (#74337) @100001 @100002 // #74337 means ~The final Battle with Bodhi~ in BG2EE's dialog.tlk. In short, we give our journal entries a name, but only for BG2EE.
END

// This code is also used by BG2 Fixpack - it would help you to run custom music for your NPC from "a clean slate", installing "blank" music you could run it with. Not everyone installs the Fixpack, though, so here we go:
// Feel free to skip this code if you're not using any music in your mod, only your NPC's voice.
COPY_EXISTING ~songlist.2da~ ~override~
SET_2DA_ENTRY 2 1 2 ~Blank~
SET_2DA_ENTRY 2 2 2 ~Blank.mus~
// You will note there's a Blank.mus file in your Audio directory - it's needed exactly for that purpose. Delete it if you're not using any music in your mod.

// Now we are going to add Erevain content for the Throne of Bhaal. You need one more cre file, or you could use the same one twice.
ACTION_IF FILE_EXISTS_IN_GAME ~mel01.cre~ THEN // It means that the Throne of Bhaal is installed. If not, this part is not added to the game. This check also works for BG2:EE games.
BEGIN // you WILL need an END below.

COPY ~Erevain/Characters/O#Bran25.cre~ ~override/O#Bran25.cre~ // Traditionally, all Throne of Bhaal files have "25" in it. You can use the same .cre file or two files. Below we patch Erevain's ToB .cre file.
SAY NAME1 ~Erevain~
SAY NAME2 ~Erevain~
SAY INITIAL_MEETING ~By the ice breath of Auril! 'Tis good to see new faces and taste freedom again.~ [o#bran01] // This text in square brackets means that BRANW01.wav is assigned here.
SAY MORALE ~Tempus, forgive my cowardice.~ [o#bran02]
SAY HAPPY ~I am proud to stand among such strong warriors.~ [o#bran04] 
SAY UNHAPPY_ANNOYED ~Our actions are not those of warrior born.~ [o#bran05] 
SAY UNHAPPY_SERIOUS ~What sort of craven cowards are we?~ [o#bran06]
SAY UNHAPPY_BREAKING ~I can no longer honorably work with such as you.~ [o#bran07]
SAY LEADER ~I have always dreamt of leading such a worthy band.~ [o#bran08]
SAY TIRED ~I'm tired.~ [o#bran09]
SAY BORED ~Damn the icicles of Auril. I'm BORED.~ [o#bran10]
SAY BATTLE_CRY1 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY2 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY3 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY4 ~Tempus! Give us victory!~ [o#bran03]
SAY BATTLE_CRY5 ~Tempus! Give us victory!~ [o#bran03] // Erevain BG1 has very few sound files, so I have to use the same line over and over. You can use five different ones, however.
SAY DAMAGE ~~ [o#bran38] // There's no text when Erevain cries out in pain.
SAY DYING ~~ [o#bran39]
SAY HURT ~'Tis but a flesh wound.~ [o#bran11]
SAY AREA_FOREST ~I am always at ease in the forest. It reminds me of home.~  // If there's no sound file for the line, then it's going to be silent.
SAY AREA_CITY ~By Tempus, I love the big city!~
SAY AREA_DUNGEON ~Danger is about. Be wary, friends.~
SAY AREA_DAY ~Ah, I love traveling in fair weather.~
SAY AREA_NIGHT ~I can't see a thing. Are we craven thieves that we do not sleep?~
SAY SELECT_COMMON1 ~By Tempus' shield.~ [o#bran17]
SAY SELECT_COMMON2 ~What is your command?~ [o#bran18]
SAY SELECT_COMMON3 ~Hello?~ [o#bran19]
SAY SELECT_COMMON4 ~By Tempus' shield.~ [o#bran17]
SAY SELECT_COMMON5 ~What is your command?~ [o#bran18]
SAY SELECT_COMMON6 ~Hello?~ [o#bran19]
SAY SELECT_ACTION1 ~'Tis a fine day to die.~ [o#bran20]
SAY SELECT_ACTION2 ~If Tempus wills it.~ [o#bran21]
SAY SELECT_ACTION3 ~By Valkur's mighty blade.~ [o#bran22]
SAY SELECT_ACTION4 ~'Tis better to die on your feet, than live on your knees.~ [o#bran23]
SAY SELECT_ACTION5 ~May Auril bestow the frost kiss upon our enemies.~ [o#bran24]
SAY SELECT_ACTION6 ~By Valkur's strapping buttocks.~ [o#bran25]
SAY SELECT_ACTION7 ~A god's favor is fickle, at best.~ [o#bran26]
SAY SELECT_RARE1 ~To die in battle is the most glorious death of all.~ [o#bran40]
SAY SELECT_RARE2 ~Tempus would guide your wayward soul, if you let him.~ [o#bran30]
SAY CRITICAL_HIT ~Tempus!~
SAY CRITICAL_MISS ~Argh!~
SAY TARGET_IMMUNE ~Another weapon! Now!~
SAY INVENTORY_FULL ~I can carry no more. I had to drop it.~ 
SAY SPELL_DISRUPTED ~My spell!~
SAY BIO ~When asked about her past, Erevain reveals that she left her home of Seawolf in the Norheim isles at an early age. She was apparently reviled by her own people for wanting to become a priest, a station reserved exclusively for the men of the village. She harbors no resentment, realizing that her faith must face such tests or be worthless. She traveled the coast, offering her services to local militias, and eventually came into the employ of an adventuring troup in the Nashkel area. Erevain had no idea that the group was participating in banditry, however, and on their first raid she balked at attacking a caravan of unarmed merchants. She rebelled, and a mage named Tranzig turned her to stone in the ensuing fight. Despite the unfavorable outcome, Erevain feels that Tempus views her actions approvingly. To attack those that are unarmed is the basest of villainy.~
WRITE_ASCII 0x248 ~O#Bra25S~ #8  // override script where you put all the commands for lovetalks and such.
WRITE_ASCII 0x2cc ~O#Bran25~ #8  // dialogue file where we'll put Erevain's greeting dialogue for ToB, if you summon her via Fate Spirit.
WRITE_ASCII 0x280 ~O#Bran~   #32 // death variable - it remains the same in ToB.
WRITE_ASCII 0X34  ~BRANWES~  #8  // small portrait
WRITE_ASCII 0x3c  ~BRANWEM~  #8  // medium portrait

COMPILE ~Erevain/Dialogue/O#BBra25.d~  // All Erevain's ToB banters with other party members will go here. Again, we use 25, because it's a tradution for ToB.
	~Erevain/Dialogue/O#Bran25.d~  // Erevain's ToB greeting dialogue will go here.
	~Erevain/Dialogue/O#Bra25J.d~  // The rest of Erevain's ToB dialogue: her commentary, interjections, talks with the player, lovetalks and everything else.
	~Erevain/Dialogue/O#Bra25P.d~  // Erevain's ToB leaving party dialogue will go here.

	~Erevain/Scripts/O#Bra25S.baf~ // Erevain's script for ToB.
	~Erevain/Scripts/O#Bra25D.baf~ // Erevain's dream script for ToB.

// Baldur's Gate 2: Enhanced Edition adds four new NPCs for your party. Naturally, Erevain banters with these NPCs, too. Let's compile a separate banter file here in ToB section(because it involves Erevain's ToB banter file):
ACTION_IF GAME_IS ~BG2EE~ BEGIN
COMPILE	~Erevain/Dialogue/BranEE.d~
END

// Erevain is going to be romanceable, so she'll need a Gorion Wraith sequence. We are going to copy Anomen's sister Moira and turn her into Erevain's grandmother. Like this:
COPY_EXISTING ~loveone4.cre~ ~override/o#brang.cre~
SAY NAME1 ~Erevain's Grandmother~
SAY NAME2 ~Erevain's Grandmother~
WRITE_ASCII 0x248 ~o#brang~ #8	// override
WRITE_ASCII 0x2cc ~o#brang~ #8  // dialogue
WRITE_ASCII 0x280 ~o#brang~ #32 // death variable
WRITE_ASCII 0x250 ~~ 	    #8  // class script
WRITE_ASCII 0x258 ~~ 	    #8  // race script
WRITE_ASCII 0x260 ~~ 	    #8  // general script
WRITE_ASCII 0x268 ~~	    #8	// default script

// And this is the extra script for Gorion's Wraith:
EXTEND_TOP ~Cut218g.bcs~  ~Erevain/Scripts/Cut218g.baf~

// This is the cutscene for Erevain's grandmother to appear in the scene with Gorion's wraith:
COMPILE ~Erevain/Scripts/O#BranG.baf~

// Since we want our NPCs to have an epilogue, we'll add something to the last area in the game:
EXTEND_TOP ~AR6200.bcs~ ~Erevain/Scripts/AR6200.baf~

// It's an epilogue file. Just copy it for your own needs. Please note that the name of the large portrait for your NPC should be in the upper left corner instead of BranweL.
COPY ~Erevain/Epilogue/O#Brazz.2da~ ~override/O#Bra01.2da~
REPLACE ~99999~ ~After the events of the Throne of Bhaal were over, Erevain remained by her friends' side. Their new adventures together rivaled those of Drizzt himself, and indeed, if rumors were true, the famous drow ranger joined them once outside Silverymoon.

Several years later, however, Erevain started yearning for home, and eventually decided to visit her old home village. A holy warrior and a high priestess of Tempus, she was greeted warmly by most of the townsfolk, and remained there for a while.

But adventure beckoned, and soon Erevain joined her friends again, fighting for good and noble ends. Many men and women across the Realms would remember the fair-haired woman with a smile, often recalling her favourite saying: "By Tempus' shield!"~

// Since Erevain is romanceable, we need a second epilogue file for her romance. We copy over the same file under a different name and replace the epilogue text.
COPY ~Erevain/Epilogue/O#Brazz.2da~ ~override/O#Bra02.2da~
REPLACE ~99999~ ~Erevain and <CHARNAME> would cross the Realms thrice over, from Kara-Tur to Norheim Isles. Ever together, they would battle dragons and tame demons, match wits with kings and drink with rascals.

They would keep winning admiration of the common folk and respect of the underworld alike, and yet always they would have a moment for one another.

Eventually Erevain and <CHARNAME> would settle down for a time, but some new adventure would always beckon, and, laughing, they would don their traveling garb and take to the road again.

Indeed, many roads they would travel, and many more would be left to discover. "If Tempus wills it," Erevain would once again say with a smile, taking her lover's hand. "What is your command?"~

END // and here ends ToB content

// These files add Erevain's scripts and dialogue files to the game(except for the banter file, that's below). The first block is for SoA-only installs, the second one is for ToB.
// Here's the order: death variable, leaving party file, main dialogue file(J for joined), dream script for SoA.
// Or death variable, leaving party file, main dialogue file(J for joined), dream script for SoA, leaving party file for ToB, main dialogue(J) file for ToB, ToB dream script, ToB main script.

APPEND ~pdialog.2da~
~O#Bran        O#BranP             O#BranJ            O#BranD~
UNLESS ~O#Bran~
UNLESS ~25POST~

APPEND ~pdialog.2da~
~O#Bran        O#BranP             O#BranJ            O#BranD             O#Bra25P             O#Bra25J             O#Bra25D       O#Bra25S~
UNLESS ~O#Bran~
IF ~25POST~

// These files add Erevain's banter files to the game. The first block is for SoA-only installs, the second one is for ToB.
// Here's the order: death variable, SoA banter file for SoA. Or death variable, SoA banter file, ToB banter file for ToB.
APPEND ~interdia.2da~
~O#Bran       O#BBran~
UNLESS ~O#Bran~
UNLESS ~25FILE~

APPEND ~interdia.2da~
~O#Bran       O#BBran       O#BBra25~
UNLESS ~O#Bran~
IF ~25FILE~

// Some modders use README flag instead. Not me. Every user is presented with a readme file so that they know where to find Erevain, whether she is romanceable and so on.
AT_INTERACTIVE_EXIT ~VIEW Erevain/Readme-Erevain.txt~

// If you want to use README flag instead, use this, just place it in the top part of your file, right after VERSION line but before BEGIN line, and uncomment it. Your players will be asked whether they want to read a readme:
// README ~Erevain/Readme-Erevain.txt~

// This is really all there is about a simple NPC mod. Since you will probably want your mod to be translated into multiple languages, you can read on the forums about translation files, or .tra files(files with strings that look like @100 = ~Hello!~). This mod is not fully traified for ease-of-use reasons - Erevain NPC is a tutorial of sorts, after all, so her files should be easy enough to read.

